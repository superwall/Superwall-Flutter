group 'com.superwall.superwallkit_flutter'
version '1.0-SNAPSHOT'

buildscript {
    repositories { mavenCentral() }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-serialization:1.6.0"
    }
}

allprojects {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

android {
    if (project.android.hasProperty("namespace")) {
        namespace 'com.superwall.superwallkit_flutter'
    }

    compileSdkVersion 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    defaultConfig {
        minSdkVersion 26
    }

    buildTypes {
        release {
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()

            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }

    // Read from local.properties
    def localPropertiesFile = rootProject.file('local.properties')
    def localProperties = new Properties()
    if (localPropertiesFile.exists()) {
        localPropertiesFile.withInputStream { localProperties.load(it) }
    }

    // Set a default value if the property is not found
    def waitForDebugger = localProperties.getProperty('WAIT_FOR_DEBUGGER', 'false')

    defaultConfig {
        buildConfigField "boolean", "WAIT_FOR_DEBUGGER", waitForDebugger
    }
}
android.buildFeatures.buildConfig true
dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.mockito:mockito-core:5.0.0'
    
    implementation "com.superwall.sdk:superwall-android:2.6.5"
    implementation 'com.android.billingclient:billing:6.1.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.2'
}